import groovy.json.JsonSlurper

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    ext {
        json = new JsonSlurper().parseText(new File("/home/sbeve72/keystore/keystore_password.json").text)
        keystore_path = json[0].keystore_path
        keystore_password = json[0].keystore_password
        key_alias = json[0].keys[1].key_alias
        key_password = json[0].keys[1].key_password
    }

    signingConfigs {
        release {
            storeFile file(keystore_path)
            storePassword keystore_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    defaultConfig {
        applicationId "com.sbeve.colorpal"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 3
        versionName "v1.1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    //uncategorized
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"

    //material design
    implementation "com.google.android.material:material:$material_components_version"

    //navigation components
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //architecture components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    //coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //palette
    implementation 'androidx.palette:palette-ktx:1.0.0'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    //dagger-hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
}
